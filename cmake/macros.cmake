# DEFINE MODIFIED MACRO FOR CREATING/LINKING PDAL PLUGINS
MACRO(PDAL_CREATE_PLUGIN)
    SET(options)
    SET(oneValueArgs TYPE NAME VERSION)
    SET(multiValueArgs SOURCES LINK_WITH INCLUDES)
    cmake_parse_arguments(PDAL_CREATE_PLUGIN "${options}" "${oneValueArgs}" "${multiValueArgs}"
        ${ARGN} )

    STRING(TOLOWER ${PDAL_CREATE_PLUGIN_TYPE} TYPE)
    STRING(TOLOWER ${PDAL_CREATE_PLUGIN_NAME} NAME)
    SET(VERSION ${PDAL_CREATE_PLUGIN_VERSION})
    SET(SOURCES "${PDAL_CREATE_PLUGIN_SOURCES}")
    IF (NOT TYPE STREQUAL "reader" AND
        NOT TYPE STREQUAL "writer" AND
        NOT TYPE STREQUAL "filter" AND
        NOT TYPE STREQUAL "kernel")
        MESSAGE(FATAL_ERROR "Invalid plugin type '${TYPE}'. Must be 'reader', 'writer', 'filter' or 'kernel'.")
    ENDIF()
    SET(PROJECT_NAME ${NAME}_${TYPE})
    SET(TARGET libpdal_plugin_${TYPE}_${NAME})

    PROJECT(${PROJECT_NAME} VERSION ${VERSION} LANGUAGES CXX)

    ADD_LIBRARY(${TARGET} SHARED ${SOURCES})
    SET_PROPERTY(TARGET ${TARGET} PROPERTY CXX_STANDARD 17)
    SET_PROPERTY(TARGET ${TARGET} PROPERTY CXX_STANDARD_REQUIRED TRUE)
    TARGET_INCLUDE_DIRECTORIES(${TARGET} PRIVATE 
        ${PDAL_CREATE_PLUGIN_INCLUDES}
    )
    TARGET_LINK_LIBRARIES(${TARGET} PRIVATE 
        ${PDAL_CREATE_PLUGIN_LINK_WITH}
    )
ENDMACRO()